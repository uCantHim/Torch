cmake_minimum_required(VERSION 3.16)

if (UNIX AND NOT APPLE)
    set(LINUX TRUE)
endif()

project(Torch)

# Options
option(TORCH_FLIP_Y_AXIS "Build Torch in a configuration that has Vulkan's negative y-axis flipped" ON)
option(TORCH_BUILD_TEST "Build tests for torch" ON)
option(TORCH_DEBUG "Build with debug flags and -logging" OFF)
option(TORCH_USE_FBX_SDK "Toggle use of the FBX SDK and related features. Requires libxml2!" ON)
option(TORCH_INTEGRATE_IMGUI "Use imgui integration" OFF)
option(TORCH_INTEGRATE_GUI "Build integration of custom GUI library" ON)
option(TORCH_OPTIMIZE_SHADERS "Enable optimization for shader compilation" ON)
option(TORCH_BUILD_SHADER_COMPILER "Build Torch's custom shader compiler" OFF)
option(TORCH_BUILD_SCENE_EDITOR "Build a graphical scene editing tool" OFF)

set(TORCH_EXECUTABLE_OUTPUT_PATH ${CMAKE_CURRENT_BINARY_DIR}/bin)
set(TORCH_SHADER_DIR ${CMAKE_CURRENT_LIST_DIR}/shaders)

# TODO: Change shader builds to out-of-source when all shaders are switched
# to be generated by the pipeline compiler
set(TORCH_SHADER_OUTPUT_DIR ${TORCH_SHADER_DIR})
# set(TORCH_SHADER_OUTPUT_DIR ${CMAKE_BINARY_DIR}/generated-sources/shaders)

set(TORCH_CONFIG_DIR ${CMAKE_CURRENT_LIST_DIR}/config)
set(TORCH_GENERATED_DIR ${CMAKE_BINARY_DIR}/generated-sources/include/trc)

if (${TORCH_DEBUG})
    set(CMAKE_BUILD_TYPE DEBUG)
endif()

# Other project settings
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

# Debug settings
set(TORCH_DEBUG $<$<CONFIG:Debug>:ON>)

# Required third-party
include(FetchContent)
include(cmake/utils.cmake)

find_package(Freetype REQUIRED)
find_package(Protobuf REQUIRED)
find_package(PNG REQUIRED)

if (TARGET profiler)
    set(HAS_PROFILER TRUE)
endif()

# Tools
add_subdirectory(torch_util)
add_subdirectory(tools)
add_subdirectory(shaders)

add_custom_command(
    OUTPUT ${CMAKE_CURRENT_LIST_DIR}/.git/hooks/pre-commit
    COMMAND ln -svf ../../tools/git-hooks/pre-commit ${CMAKE_CURRENT_LIST_DIR}/.git/hooks/pre-commit
)
add_custom_target(install_git_hooks ALL DEPENDS ${CMAKE_CURRENT_LIST_DIR}/.git/hooks/pre-commit)

# Library target
add_library(torch)
torch_default_compile_options(torch)

add_dependencies(torch compile_shaders)

target_include_directories(torch
    PUBLIC
        ${CMAKE_CURRENT_LIST_DIR}/include
        ${CMAKE_CURRENT_LIST_DIR}/include/trc
    PRIVATE
        ${CMAKE_CURRENT_LIST_DIR}/include/trc
        ${CMAKE_CURRENT_LIST_DIR}/include/trc/core
)

target_link_libraries(
    torch
    PUBLIC
        torch_util
        component
        vk_base
        Freetype::Freetype
        protobuf::libprotobuf
        $<$<AND:$<BOOL:${HAS_PROFILER}>,$<CONFIG:Debug>>:profiler>
    PRIVATE
        PNG::PNG
)

target_compile_definitions(torch
    PUBLIC
        TRC_COMPILE_ROOT_DIR="${CMAKE_CURRENT_LIST_DIR}"
        TRC_SHADER_DIR="${TORCH_SHADER_OUTPUT_DIR}"

        $<$<BOOL:${TORCH_FLIP_Y_AXIS}>:TRC_FLIP_Y_PROJECTION>
        $<$<BOOL:${TORCH_DEBUG}>:TRC_DEBUG>

    INTERFACE
        TRC_USE_VKB_NAMESPACE  # Integrate vkb into trc when building for the user
)

# Pipeline generation
include(tools/pre_build_step/CMakeLists.txt)
_torch_compile_pipelines_cpp("Common")
_torch_compile_pipelines_cpp("DrawablePipelines")
_torch_compile_pipelines_cpp("RasterPipelines")

# Protobuf generation
include(protobuf/CMakeLists.txt)

# Sources
add_subdirectory(vk_base)
add_subdirectory(src)
add_subdirectory(include/trc) # Precompiled headers

# Imgui integration
if (${TORCH_INTEGRATE_IMGUI})
    target_compile_definitions(torch PUBLIC TRC_USE_IMGUI)

    add_library(imgui)
    target_link_libraries(torch PUBLIC imgui)

    target_include_directories(
        imgui PUBLIC
        ${CMAKE_CURRENT_LIST_DIR}/external/imgui
        ${CMAKE_CURRENT_LIST_DIR}/external/imgui/backends
    )
    target_sources(
        imgui PRIVATE
        ${CMAKE_CURRENT_LIST_DIR}/external/imgui/imgui.cpp
        ${CMAKE_CURRENT_LIST_DIR}/external/imgui/imgui_draw.cpp
        ${CMAKE_CURRENT_LIST_DIR}/external/imgui/imgui_tables.cpp
        ${CMAKE_CURRENT_LIST_DIR}/external/imgui/imgui_widgets.cpp

        ${CMAKE_CURRENT_LIST_DIR}/external/imgui/backends/imgui_impl_glfw.cpp
        ${CMAKE_CURRENT_LIST_DIR}/external/imgui/backends/imgui_impl_vulkan.cpp
    )
endif ()

# Custom GUI library
if (${TORCH_INTEGRATE_GUI})
    add_subdirectory(src/ui)
endif ()

# Test
if (${TORCH_BUILD_TEST})
    add_subdirectory(test)
endif ()

# Optional FBX stuff
if (${TORCH_USE_FBX_SDK})
    target_link_libraries(torch PUBLIC fbxsdk xml2)
    target_compile_definitions(torch PUBLIC TRC_USE_FBX_SDK)
endif ()

# Optional assimp stuff
cmake_policy(PUSH)
cmake_policy(SET CMP0012 NEW)
find_package(assimp)
if (${assimp_FOUND})
    target_link_libraries(torch PUBLIC ${ASSIMP_LIBRARIES})
    target_compile_definitions(torch PUBLIC TRC_USE_ASSIMP)
endif ()
cmake_policy(POP)
