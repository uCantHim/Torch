Meta:
    Namespace: "trc"


enum AnimationType: none, boneAnim

enum PipelineShadingType:
    opaque,
    transparent,
    shadow


Shader assetRegistryDescriptor:
    Source: "asset_registry_descriptor_template.glsl"
    Target: "asset_registry_descriptor.glsl"
    TargetType: glsl

    Variable material_descriptor_binding: "0"
    Variable texture_descriptor_binding: "1"
    Variable geometry_index_descriptor_binding: "2"
    Variable geometry_vertex_descriptor_binding: "3"
    Variable animation_meta_descriptor_binding: "4"
    Variable animation_data_descriptor_binding: "5"


Shader drawableFragment:
    Source: match PipelineShadingType
        opaque -> "drawable/deferred.frag"
        transparent -> "drawable/transparent.frag"
        shadow -> "empty.frag"

Shader shadowVertex:
    Source: match AnimationType
        none -> "drawable/shadow.vert"
        boneAnim -> "drawable/shadow_animated.vert"

Shader defaultVertex:
    Source: match AnimationType
        none -> "drawable/deferred.vert"
        boneAnim -> "drawable/deferred_animated.vert"

Shader drawableVertex: match PipelineShadingType
    opaque -> defaultVertex
    transparent -> defaultVertex
    shadow -> shadowVertex

Program drawableProgram:
    VertexShader: drawableVertex
    FragmentShader: drawableFragment


VertexAttribute drawableVertexInput:
    Binding: 0
    InputRate: perVertex
    Locations: match AnimationType
        none -> [rgb32f, rgb32f, rg32f, rgb32f]
        boneAnim -> [rgb32f, rgb32f, rg32f, rgb32f, rgba32u, rgba32f]


///////////////
//  Layouts  //
///////////////

PushConstant drawablePushConstants:
    Offset: 0
    Size: 84
    DefaultValue: "drawablePushConstantDefaultValue"

Layout drawableLayout:
    Descriptors: match PipelineShadingType
        opaque -> ["global_data", "asset_registry", "scene_data", "g_buffer"]
        transparent -> ["global_data", "asset_registry", "scene_data", "g_buffer", "shadow"]
        // shadow -> match AnimationType
        //     none -> ["shadow"]
        //     boneAnim -> ["shadow", "asset_registry"]
        shadow -> ["shadow", "asset_registry"]

    VertexPushConstants: [drawablePushConstants]


////////////////
//  Pipeline  //
////////////////

Pipeline drawablePipeline:
    Program: drawableProgram
    Layout: drawableLayout
    RenderPass: match PipelineShadingType
        opaque -> "g_buffer"
        transparent -> "transparency"
        shadow -> "shadow"
    VertexInput: [drawableVertexInput]
    DisableBlendAttachments: 3
